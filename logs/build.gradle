plugins {
    id 'com.github.johnrengelman.shadow'
    id 'application'
    id 'scala'
    id 'java'
    id 'idea'
}

apply plugin: 'maven-publish'

group 'com.flink.logs'

sourceSets {
    seedKafka
}

dependencies {
    implementation project(":common")
    implementation "org.scala-lang:scala-library:$scala_version"
    implementation "org.apache.flink:flink-streaming-scala_$scala_version:$flink_version"
    implementation "org.apache.flink:flink-connector-kafka:$flink_version"
    implementation "org.apache.flink:flink-connector-jdbc:$flink_version"
    implementation "org.apache.flink:flink-table:$flink_version"
    implementation "org.postgresql:postgresql:$postgres_version"
    implementation "org.flywaydb:flyway-core:$flyway_version"
    implementation "org.clapper:grizzled-slf4j_$scala_version:$grizzled_slf4j_version"
    implementation "com.github.tototoshi:scala-csv_$scala_version:$csv_version"
    implementation "org.json4s:json4s-jackson_$scala_version:$json4s_version"

    testImplementation "org.scalatest:scalatest_$scala_version:$scalatest_version"
    testImplementation "org.scalamock:scalamock_$scala_version:$scalamock_version"

    seedKafkaImplementation sourceSets.test.runtimeClasspath
    seedKafkaImplementation sourceSets.main.runtimeClasspath
    seedKafkaImplementation "org.apache.commons:commons-compress:$commons_compress_version"
}

assemble.dependsOn(shadowJar)

application {
    mainClassName = 'com.flink.logs.Application'
}

shadowJar {
    mainClassName = 'com.flink.logs.Application'
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': mainClassName
    }
    zip64 true
}

task seedKafka(type: Test) {
    testClassesDirs = sourceSets.seedKafka.output.classesDirs
    classpath = sourceSets.seedKafka.runtimeClasspath
    systemProperties System.getProperties()
}

task seed(type: JavaExec, dependsOn: seedKafkaClasses) {
    main = 'com.flink.logs.SeedKafka'
    args = "-Xmx2000M -Xms2000M".split().toList()
    classpath sourceSets.seedKafka.runtimeClasspath
}
